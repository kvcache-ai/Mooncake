cmake_minimum_required(VERSION 3.12)

option(METASTORE_BUILD_SHARED "Build shared library for metastore" OFF)

function(add_metastore_module name)
    set(options)
    set(oneValueArgs)
    set(multiValueArgs DEPS)
    cmake_parse_arguments(RES "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
    if(METASTORE_BUILD_SHARED)
        add_library(${name} SHARED ${name}.cpp)
    else()
        add_library(${name} STATIC ${name}.cpp)
    endif()
    if(RES_DEPS)
        target_link_libraries(${name} PUBLIC ${RES_DEPS})
    endif()
endfunction()

if(USE_ETCD)
    set(ETCD_WRAPPER_LIB ${CMAKE_CURRENT_BINARY_DIR}/../../../mooncake-common/etcd/libetcd_wrapper.so)
    add_metastore_module(etcd DEPS ${ETCD_WRAPPER_LIB})
    add_dependencies(etcd build_etcd_wrapper)
endif()

if(USE_HTTP)
    find_package(CURL REQUIRED)
    add_metastore_module(http DEPS CURL::libcurl)
endif()

if(USE_REDIS)
    find_library(HIREDIS_LIB hiredis REQUIRED)
    add_metastore_module(redis DEPS ${HIREDIS_LIB})
endif()
